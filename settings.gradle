pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == "ru.netris.commitlint") {
                useModule("ru.netris:commitlint-plugin:${requested.version}")
            }
        }
    }
}
plugins {
    id 'nu.studer.credentials' version '3.0'
}
ext {
    def nexusUrl = credentials.forKey('nexusUrl')
    def nexusId = credentials.forKey('nexusId')
    def nexusPassword = credentials.forKey('nexusPassword')
    gradle.ext.nexusUrl = nexusUrl
    gradle.ext.nexusId = nexusId
    gradle.ext.nexusPassword = nexusPassword
}

rootProject.name = 'mco-playground'
[
        //------ application ------
        "app:zoo",
        "app:grassland",

        //------ internal shared ------
        "rep:shared"
].each {
    if (it.indexOf("global") != -1) {
        def dir = makeDir(rootDir, it)
        intiProjectFolder(dir, it);
    } else {
        def projectNameArr = it.split(":")

        def prefixName = projectNameArr[0]
        def projectName = projectNameArr[1]
        def dir = makeDir(rootDir, prefixName)
        def subDir = makeDir(dir, projectName)
        intiProjectFolder(subDir, it);
    }

}


def intiProjectFolder(dir, projectFullName) {
    // add .gitignore file
    def gitFile = new File(dir.absolutePath, ".gitignore")
    if (!gitFile.exists()) {
        gitFile.append("build/")
        gitFile.append("\nout/")
        gitFile.append("\ngenerated/")
    }

    def gradleFile = new File(dir.absolutePath, "build.gradle")
    if (!gradleFile.exists()) {
        gradleFile.text =
                """

                    dependencies {

                    }

                    """.stripIndent(20)
    }
    [
            "src/main/java/com/mco",
            "src/main/resources",
            "src/test/java/com/mco",
            "src/test/resources"

    ].each { srcDir ->
        def srcFolder = new File(dir.absolutePath, srcDir)
        if (!srcFolder.exists()) {
            srcFolder.mkdirs()
        }
    }
    def projectName = ":${projectFullName}"

    include projectName
    project(projectName).projectDir = dir
}


def makeDir(attachedDir, name) {
    def dir = new File(attachedDir, name)
    if (!dir.exists()) {
        dir.mkdirs()
    }
    return dir;
}
